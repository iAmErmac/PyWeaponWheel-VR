Class Py_WeaponWheelEventHandler : EventHandler
{
	bool searchedWeps;
	array<string > avoidTheseWeapons;
	array<string > useTheseWeapons;
	
	array<Py_WeaponWheel_Weapon > wheelWeapons;
	
	array<bool > wheelShow;
	int wheelIt;
	int currentSelection;
	int oldSelection;
	array<int > actualList;
	Vector2 fakeMouse;
	Vector2 realMouse;
	int angleSave;
	Vector2 selectionMouse;
	int selAngle;
	TextureID crowbarTex;
	int oldPitch;
	int oldYaw;
	double alphaFade;
	bool nCY;
	
	bool wheelIsOpen;
	bool wheelSort;
	bool inclusiveSort;
	
	int icons_renderstyle;
	double icons_alpha[10];
	
	bool manyWeapon;
	
	Array<int> projectileVelX, projectileVelY, projectileVelZ;
	bool isLevelFrozen;
	int wheelCloseCounter;
	
	enum E_WeaponSortMode
	{
		WSM_Nothing,
		WSM_Excluding,
		WSM_Including,
		WSM_Validating,
		WSM_Hiding,
		WSM_Merging,
		WSM_ForcingIcon,
		WSM_Renaming,
		WSM_RenderStyle,
		WSM_SetAlpha,
	}
	
	enum E_AlphaSet
	{
		APS_Normal,
		APS_Inactive,
	}
	
	int point_distance(int x1,int y1,int x2,int y2)
	{
		return ((x1-x2)*(x1-x2)+(y1-y2)*(y1-y2));
	}
	int point_direction(int x1,int y1,int x2,int y2)
	{
		return int(atan2(y2-y1, x2-x1));
	}
	
	//============================================================
	//
	// initializeWheel()
	//
	//============================================================
	
	void initializeWheel()
	{
		// THIS IS WHERE THE FUN BEGINS
		
		icons_renderstyle = Style_Translucent;
		icons_alpha[APS_Normal] = 1.0;
		icons_alpha[APS_Inactive] = 0.5;
		
		PlayerInfo player = players[consolePlayer];
		
		crowbarTex = TexMan.CheckForTexture("graphics/pywheel/crowbar.png",TexMan.Type_Any);
		bool ret1;//is this a wepon in slottt???
		int ret2;//second return is slot
		int ret3;
		for(int i = 0;i<AllActorClasses.Size();i++)
		{
			let type = (class<Weapon>)(AllActorClasses[i]);
			if(type!=null)
			{
				Class<Actor> testtype = Actor.GetReplacement(type);
				if(testtype is "Weapon" && player.weapons.LocateWeapon((class<Weapon>)(testtype)))
				{
					type = (class<Weapon>)(testtype);
					bool isMine = false;
					int restrictSize = GetDefaultByType(type).RestrictedToPlayerClass.Size();
					if(restrictSize>0)
					{
						for(int b = 0;b<restrictSize;b++)
						{
							Class<Actor> nameCheck = GetDefaultByType(type).RestrictedToPlayerClass[b];
							if(nameCheck is player.mo.GetClass())
							{
								isMine=true;
							}
						}
					}
					else
					{
						isMine=true;
					}
					if(isMine)
					{
						[ret1, ret2, ret3] = player.weapons.LocateWeapon(type);
						Py_WeaponWheel_Weapon pushItIn = new("Py_WeaponWheel_Weapon");
						pushItIn.weaponClass = type;
						pushItIn.weaponSlot = ret2;
						pushItIn.weaponPriority = ret3;
						pushItIn.weaponScale = (1,1);
						pushItIn.weaponTag = GetDefaultByType(type).GetTag();
						wheelWeapons.Push(pushitIn);
					}
				}
			}
		}
		
		State weaponSpawnState;
		for(int i = 0;i<wheelWeapons.Size();i++)
		{
			TextureID wepIcon = GetDefaultByType(wheelWeapons[i].weaponClass).Icon;
			Vector2 weaponScaled = (1,1);
			weaponSpawnState = GetDefaultByType(wheelWeapons[i].weaponClass).SpawnState;
			
			if(TexMan.GetName(wepIcon)=="" || TexMan.GetName(wepIcon)~=="ALTHUDCF") //wtf
			{
				weaponScaled = GetDefaultByType(wheelWeapons[i].weaponClass).scale;
				wepIcon = weaponSpawnState.GetSpriteTexture(1,0);
			}
			
			int overflowProtection = 0;
			while(overflowProtection<25 && (TexMan.GetName(wepIcon)=="TNT1A0" || TexMan.GetName(wepIcon)=="NULLA0") && weaponSpawnState.NextState!=null)
			{
				weaponSpawnState = weaponSpawnState.NextState;
				wepIcon = weaponSpawnState.GetSpriteTexture(1,0);
				overflowProtection++;
			}
			
			if(TexMan.GetName(wepIcon)=="TNT1A0" || TexMan.GetName(wepIcon)=="NULLA0")
			{
				weaponScaled = (0.3,0.3);
				weaponSpawnState = GetDefaultByType(wheelWeapons[i].weaponClass).ResolveState("Ready");
				wepIcon = weaponSpawnState.GetSpriteTexture(1,0);
			}
			
			//ready state
			
			overflowProtection = 0;
			while(overflowProtection<25 && (TexMan.GetName(wepIcon)=="TNT1A0" || TexMan.GetName(wepIcon)=="NULLA0") && weaponSpawnState.NextState!=null)
			{
				weaponSpawnState = weaponSpawnState.NextState;
				wepIcon = weaponSpawnState.GetSpriteTexture(1,0);
				overflowProtection++;
			}
			
			if(TexMan.GetName(wepIcon)=="TNT1A0" || TexMan.GetName(wepIcon)=="NULLA0")
			{
				weaponScaled = (0.75,0.75*0.8);
				wepIcon = crowbarTex;
			}
			wheelWeapons[i].weaponIcon = wepIcon;
			wheelWeapons[i].weaponScale = (weaponScaled.x,weaponScaled.y);
		}
		
		//=====================================
		// begin the tokenization my friend
		//=====================================

		array<string > mergeTheseWeapons1;
		array<string > mergeTheseWeapons2;
	
		string lumpy = "";
		for(int lump = -1; (lump = Wads.FindLump("PYWHEEL", lump + 1)) != -1;)
		{
			lumpy.AppendFormat("%s", Wads.ReadLump(lump));
			lumpy.AppendFormat("\n");
		}
		
		PyWheel_Tokenizer tokenitup = PyWheel_Tokenizer.Create(lumpy);
		PyWheel_Token tokenboy = tokenitup.GoThru();
		int safetyNet = 0;
		bool gotoit;
		bool removingWeps = false;
		int sortMode = 0;
		string stringHold1 = "";
		string stringHold2 = "";
		int inthold = 0;
		double doubleHold = 0; //watch your motherfuckin tone boy
		bool jesusChristItsHim = true;
		while(tokenboy.type!=PTK_EOF && safetyNet<MAXITERATIONS)
		{
			if(tokenboy.type==PTK_Slash)
			{
				tokenboy = tokenitup.GoThru();
				safetyNet++;
				continue;
			}
			else if(removingWeps)
			{
				if(tokenboy.type==PTK_Semicolon)
				{
						if(sortMode==WSM_Merging)//Infinity War is the most amitious crossover event-
						{
							mergeTheseWeapons1.Push(stringHold1);
							mergeTheseWeapons2.Push(stringHold2);
						}
						else if (sortMode==WSM_ForcingIcon)
						{
							for(int i = 0;i<wheelWeapons.Size();i++)
							{
								if(wheelWeapons[i].weaponClass.GetClassName()==stringHold1)
								{
									if(stringHold2!="*") //special :)
									{
										wheelWeapons[i].weaponIcon = TexMan.CheckForTexture(stringHold2,TexMan.Type_Any);
									}
									wheelWeapons[i].weaponScale = GetDefaultByType(wheelWeapons[i].weaponClass).scale;
									if(doubleHold!=0)
									{
										wheelWeapons[i].weaponScale *= doubleHold;
									}
								}
							}
						}
						else if (sortMode==WSM_Renaming)
						{
							for(int i = 0;i<wheelWeapons.Size();i++)
							{
								if(wheelWeapons[i].weaponClass.GetClassName()==stringHold1)
								{
									wheelWeapons[i].weaponTag = stringHold2;
								}
							}
						}
						else if (sortMode==WSM_RenderStyle)
						{
							if(stringHold1~=="Style_Add")
							{
								icons_renderstyle = Style_Add;
							}
						}
						else if (sortMode==WSM_SetAlpha)
						{
							icons_alpha[inthold] = doublehold;
						}
						else if (sortMode==WSM_Hiding)
						{
							for(int i = 0;i<wheelWeapons.Size();i++)
							{
								if(wheelWeapons[i].weaponClass.GetClassName()==stringHold1)
								{
									wheelWeapons[i].hidden = true;
								}
							}
						}
					sortMode=WSM_Nothing;
					stringHold1 = "";stringHold2 = "";
					doubleHold = 0;
					intHold = 0;
				}
				else if(tokenboy.type==PTK_Number)
				{
					if(sortMode==WSM_ForcingIcon)
					{
						doubleHold = double(tokenboy.value.ToDouble());
					}
					else if(sortMode==WSM_SetAlpha)
					{
						doubleHold = double(tokenboy.value.ToDouble());
					}
				}
				else if(tokenboy.type==PTK_Identifier)
				{
					if(sortMode!=WSM_Nothing)
					{
						ThrowAbortException("Unexpected identifier "..tokenboy.value);
					}
					if(tokenboy.value=="exclude")
					{
						sortMode=WSM_Excluding;
					}
					else if (tokenboy.value=="include")
					{
						sortMode=WSM_Including;
					}
					else if(tokenboy.value=="merge")
					{
						sortMode=WSM_Merging;
					}
					else if(tokenboy.value=="forceicon")
					{
						sortMode=WSM_ForcingIcon;
					}
					else if(tokenboy.value=="rename")
					{
						sortMode=WSM_Renaming;
					}
					else if(tokenboy.value=="validate")
					{
						sortMode=WSM_Validating;
					}
					
					// 3.0
					
					else if (tokenboy.value=="renderstyle")
					{
						sortMode = WSM_RenderStyle;
					}
					else if (tokenboy.value=="alpha_normal")
					{
						sortMode = WSM_SetAlpha;
						inthold = APS_Normal;
					}
					else if (tokenboy.value=="alpha_inactive")
					{
						sortMode = WSM_SetAlpha;
						inthold = APS_Inactive;
					}
					else if (tokenboy.value=="hide")
					{
						sortMode = WSM_Hiding;
					}
				}
				else if(tokenboy.type==PTK_String)
				{
					switch(sortMode)
					{
						case WSM_Validating:
							Class<Inventory> chungus = tokenboy.value;
							if(chungus!=null && !player.mo.FindInventory(chungus))
							{
								jesusChristItsHim = false;
							}
						break;
						case WSM_Excluding:
							if(jesusChristItsHim)
							{
								avoidTheseWeapons.Push(tokenboy.value);
							}
						break;
						case WSM_Including:
							if(jesusChristItsHim)
							{
								useTheseWeapons.Push(tokenboy.value);
							}
						break;
						case WSM_Merging:
							if(stringHold1=="")
							{
								stringHold1 = tokenboy.value;
							}
							else
							{
								stringHold2 = tokenboy.value;
							}
						break;
						case WSM_ForcingIcon:
							if(stringHold1=="")
							{
								stringHold1 = tokenboy.value;
							}
							else
							{
								stringHold2 = tokenboy.value;
							}
						break;
						case WSM_Renaming:
							if(stringHold1=="")
							{
								stringHold1 = tokenboy.value;
							}
							else
							{
								stringHold2 = tokenboy.value;
							}
						break;
						
						case WSM_RenderStyle: //renderstyle
							stringHold1 = tokenboy.value;
							break;
						case WSM_Hiding: //kill it if you have to
							stringHold1 = tokenboy.value;
							break;
					}
				}
				else if (tokenboy.type==PTK_BraceC)
				{
					removingweps=false;
				}
			}
			else if(tokenboy.type==PTK_Identifier)
			{
				class<Actor> cls = tokenboy.value;
				if(cls!=null && player.mo is cls)
				{
					removingWeps = true;
				}
			}
			tokenboy = tokenitup.GoThru();
			safetyNet++;
		}
		
		//if the inclusion array is not empty then make us do the inclusive sort thing
		inclusiveSort = useTheseWeapons.Size()>0;
		
		for(int i = 0;i<wheelWeapons.Size();i++)
		{
			bool fuckThisOneInParticular = false;
			if(inclusiveSort)
			{
				bool foundMySon = false;
				for(int v = 0;v<useTheseWeapons.Size();v++)
				{
					if(useTheseWeapons[v] == wheelWeapons[i].weaponClass.GetClassName())
					{
						foundMySon = true;
						break;
					}
				}
				if(!foundMySon)
				{
					wheelWeapons.Delete(i);
					i--;
					continue;
				}
			}
			
				for(int v = 0;v<avoidTheseWeapons.Size();v++)
				{
					if(avoidTheseWeapons[v] == wheelWeapons[i].weaponClass.GetClassName())
					{
						wheelWeapons.Delete(i);
						i--;
						fuckThisOneInParticular = true;
						break;
					}
				}
				
			if(fuckThisOneInParticular)
			{
				continue;
			}
		}
		
		//=====================================
		// Now sort thme fuckerr
		//=====================================
			
		int i;
		int j;
		int flag = 1;
		Py_WeaponWheel_Weapon temp;
		int temp2;
		int temp3;
		int numLength = maxplayers; 
		for(i = 1; (i <= wheelWeapons.Size()) && flag; i++)
		{
			flag = 0;
			for (j=0; j < (wheelWeapons.Size() -1); j++)
			{
				if (wheelWeapons[j+1].weaponPriority > wheelWeapons[j].weaponPriority)
				{ 
					temp = wheelWeapons[j];  
					wheelWeapons[j] = wheelWeapons[j+1];
					wheelWeapons[j+1] = temp;
					flag = 1;
				}
			}
		}
		
		flag = 1;
		
		for(i = 1; (i <= wheelWeapons.Size()) && flag; i++)
		{
			flag = 0;
			for (j=0; j < (wheelWeapons.Size() -1); j++)
			{
				if (wheelWeapons[j+1].weaponSlot > wheelWeapons[j].weaponSlot)
				{ 
					temp = wheelWeapons[j];  
					wheelWeapons[j] = wheelWeapons[j+1];
					wheelWeapons[j+1] = temp;
					flag = 1;
				}
			}
		}
		
		flag = 1;
		
		for(i = 1; (i < wheelWeapons.Size()) && flag; i++)
		{
			for (j=0; j < (wheelWeapons.Size()); j++)
			{
				if (i!=j && wheelWeapons[i].weaponClass.GetClassName()==wheelWeapons[j].weaponClass.GetClassName())
				{
					wheelWeapons.Delete(j);
					if(i>j)
					{
						i--;
					}
					j=0;
				}
			}
		}
				
		for(int i = 0;i<wheelWeapons.Size();i++)
		{
			int foundMerge=-1;
			for(int v = 0;v<mergeTheseWeapons1.Size();v++)
			{
				if(wheelWeapons[i].weaponClass.GetClassName()==mergeTheseWeapons1[v])
				{
					foundMerge=v;
					wheelWeapons[i].mergeDir = false;
				}
				else if (wheelWeapons[i].weaponClass.GetClassName()==mergeTheseWeapons2[v])
				{
					foundMerge=v;
					wheelWeapons[i].mergeDir = true;
				}
			}
			if(foundMerge>-1)
			{
				wheelWeapons[i].weaponOrig = ((class<weapon>)(mergeTheseWeapons1[foundMerge]));
				wheelWeapons[i].weaponMerge = ((class<weapon>)(mergeTheseWeapons2[foundMerge]));
			}
			else
			{
				wheelWeapons[i].weaponOrig = (null);
				wheelWeapons[i].weaponMerge = (null);
			}
		}
		
		if(wheelWeapons.Size()>20)
		{
			manyWeapon = true;//too many to show good...
		}
			
		avoidTheseWeapons.Clear();
		mergeTheseWeapons1.Clear();
		mergeTheseWeapons2.Clear();
		searchedWeps=true;
	}
	
	bool musicFadeEnabled;
	
	override void WorldLoaded(WorldEvent e)
	{
		currentSelection = 1;
		musicFadeEnabled = CVar.GetCVar("py_weaponwheel_musicfade", players[consolePlayer]).GetBool();
	}
	
	double outQuad(double t, double b, double c, double d)
	{
		t = t / d;
		return -c * t * (t - 2) + b;
	}
	
	//============================================================
	//
	// WorldTick()
	//
	//============================================================
	
	const fadeSpeed = 0.2;
	double musicFade;
	int invertControls;
	
	enum E_InvertedControls
	{
		PY_INVERTNO = 0,
		PY_INVERTX = 1,
		PY_INVERTY = 2,
		PY_INVERTXY = 3,
	}
	
	array<int > oldPTics;
	array<int > oldPUTics;
	
	override void WorldTick()
	{
		PlayerInfo player = players[consolePlayer];
		if(!player) return;
		
		if(!searchedWeps && level.maptime>12)
		{
			initializeWheel();
		}
		if(!searchedWeps)
		{
			return;
		}
		invertControls = CVar.GetCVar("py_weaponwheel_invert", player).GetInt();
		if(musicFadeEnabled && musicFade>0)
		{
			SetMusicVolume(1.0-musicFade/2);
		}
		if(wheelIsOpen)
		{
			if(alphaFade<1.0)
			{
				alphaFade+=fadeSpeed;
			}
			if(musicFade<1.0)
			{
				musicFade+=fadeSpeed;
			}
			if(oldSelection!=currentSelection)
			{
				S_Sound("menu/change",5);
			}
			if(!wheelSort)
			{
				for(int i = 0;i<wheelWeapons.Size();i++)
				{
					bool fucked = false;
					
					// If there are too many weapons, or it is supposed to be hidden until the player has it, hide it
					if((manyweapon || wheelWeapons[i].hidden) && !player.mo.FindInventory(wheelWeapons[i].weaponClass))
					{
						fucked = true;
					}
					if( (!wheelWeapons[i].mergeDir && wheelWeapons[i].weaponMerge!=null && player.mo.FindInventory(wheelWeapons[i].weaponMerge))
					||
					(wheelWeapons[i].mergeDir && wheelWeapons[i].weaponMerge!=null && !player.mo.FindInventory(wheelWeapons[i].weaponMerge)) )
					{
						fucked = true;
					}
					if(!fucked)
					{
						wheelShow.Push(i);
					}
				}
				
				wheelSort = true;
			}
			oldSelection = currentSelection;
			
			//Let us use joystick as mouse
			let joypad_speed = 20;
			let pos = joypad_speed * (
					15.0 * player.original_cmd.sidemove		/ 10240,
				-	15.0 * player.original_cmd.forwardmove	/ 12800);
			realMouse.x = pos.x;
			realMouse.y = pos.y;
			
			if(point_distance(0,0,int(realMouse.x),int(realMouse.y))>6000)
			{
				int angler = point_direction(0,0,int(realMouse.x),int(realMouse.y));
				realMouse = (cos(angler)*80,sin(angler)*80);
			}
			fakeMouse.x = realMouse.x;
			fakeMouse.y = realMouse.y;
			if(point_distance(0,0,int(fakeMouse.x),int(fakeMouse.y))<5900)
			{
				int angler = point_direction(0,0,int(fakeMouse.x),int(fakeMouse.y));
				fakeMouse = (cos(angler)*78,sin(angler)*78);
			}
			int chooseAngle = point_direction(0,0,int(fakeMouse.x),int(-fakeMouse.y))+180;
			int averageAngles = 360/max(1,wheelShow.Size());
			int halfAngle = averageAngles/2;
			for(int i = 0;i<wheelShow.Size();i++)
			{
				int pointyAngle = int((double(i+1)/wheelShow.Size())*360);
				if(abs(Actor.DeltaAngle(chooseAngle,pointyAngle))<halfAngle)
				{
					currentSelection=i;
					break;
				}
			}
			
			//Always freeze player when wheel is open
			FreezePlayer(player);
		}
		else
		{
			if(alphaFade>fadeSpeed)
			{
				alphaFade-=fadeSpeed;
			}
			if(musicFade>fadeSpeed)
			{
				musicFade-=fadeSpeed;
			}
			
			//This is to disable invulnerability after wheel close when loaded with bullet-time-x mod
			if(wheelCloseCounter > 0)
			{
				wheelCloseCounter--;
			}
			else if(wheelCloseCounter == 0)
			{
				bool playerSlowMoInvulnerable = CVar.GetCVar("py_weaponwheel_invul", player).GetBool();
				if(playerSlowMoInvulnerable) player.mo.bInvulnerable = false;
				wheelCloseCounter = -1;
			}
		}
	}
	
	//Freeze all monsters and projectiles
	Void FreezeActors()
	{
		PlayerInfo player = players[consolePlayer];
		if(!player) return;
		
		bool useSlowMo = CVar.GetCVar("py_weaponwheel_slowmo", player).GetBool();
		bool useSlowMoSound = CVar.GetCVar("py_weaponwheel_slow_sound", player).GetBool();
		
		bool playerSlowMoInvulnerable = CVar.GetCVar("py_weaponwheel_invul", player).GetBool();
		if(playerSlowMoInvulnerable) player.mo.bInvulnerable = true; //Make player invulnerable during freeze/slow-mo
		isLevelFrozen = true;
		
		string btClassName = "BulletTime";
		class<EventHandler> btLoaded = btClassName;
		
		if(btLoaded && useSlowMo)
		{
			SendNetworkEvent("btRemoteActivate", useSlowMoSound, 20);
			return;
		}
	
		projectileVelX.Clear();
		projectileVelY.Clear();
		projectileVelZ.Clear();
			
		ThinkerIterator It = ThinkerIterator.Create();
        Actor Mo;
        While (Mo = Actor(It.Next()))
        {
            If (Mo && (Mo.bISMONSTER || Mo.bMISSILE) && mo.health > 0)
            {
                Mo.tics = -1;
				Mo.gravity = 0;
				
				if(Mo.bMISSILE)
				{
					projectileVelX.Push(Mo.vel.x);
					projectileVelY.Push(Mo.vel.y);
					projectileVelZ.Push(Mo.vel.z);
				}
				
				Mo.vel = (0, 0, 0);
            }
        }
	}
	
	//Thaw all monsters and projectiles
	Void ThawActors()
	{
		PlayerInfo player = players[consolePlayer];
		if(!player) return;
		
		bool useSlowMo = CVar.GetCVar("py_weaponwheel_slowmo", player).GetBool();
		bool useSlowMoSound = CVar.GetCVar("py_weaponwheel_slow_sound", player).GetBool();
		
		wheelCloseCounter = 35; //This counter will make sure invulnerability is disabled when wheel is closed
		isLevelFrozen = false;
		
		string btClassName = "BulletTime";
		class<EventHandler> btLoaded = btClassName;
		
		if(btLoaded)
		{
			SendNetworkEvent("btRemoteDeactivate", useSlowMoSound);
		}
		
		ThinkerIterator It = ThinkerIterator.Create();
        Actor Mo;
		Int mCount = 0;
        While (Mo = Actor(It.Next()))
        {
            If (Mo && (Mo.bISMONSTER || Mo.bMISSILE) && mo.health > 0)
            {
                Mo.tics = 1;
				Mo.gravity = Mo.default.gravity;
				if(!Mo.bMISSILE) Mo.vel = Mo.default.vel;
				
				if(Mo.bMISSILE && projectileVelX.size())
				{
					Mo.vel.x = projectileVelX[mCount];
					Mo.vel.y = projectileVelY[mCount];
					Mo.vel.z = projectileVelZ[mCount];
					mCount++;
				}
            }
        }
		
		projectileVelX.Clear();
		projectileVelY.Clear();
		projectileVelZ.Clear();
	}
	
	//Freeze player in place
	Void FreezePlayer(PlayerInfo player)
	{
		player.vel = (0, 0);
		player.mo.vel = (0, 0, 0);
		player.mo.gravity = 0;
	}
	
	//Thaw player
	Void ThawPlayer(PlayerInfo player)
	{
		player.vel = player.mo.default.vel.xy;
		player.mo.vel = player.mo.default.vel;
		player.mo.gravity = player.mo.default.gravity;
	}
	
	Override Void NetworkProcess(ConsoleEvent e)
	{
		PlayerInfo player = players[e.player];
		if(!player) return;
		
		if(e.name == 'pyweaponwheelon')
		{
            if (wheelIsOpen)
			{
				SendNetworkEvent("pyweaponwheeloff");
				return; 
			}
			wheelShow.Clear();
			wheelIsOpen = true;
			if(point_distance(0,0,int(realMouse.x),int(realMouse.y))>32)
			{
				realMouse = (0,0)+realMouse*0.1;
			}
			fakeMouse = realMouse;
			if(CVar.GetCVar("py_weaponwheel_freeze", players[e.Player]).GetBool())
			{
				freezeActors();
			}
		}
		else if(e.name == 'pyweaponwheeloff')
		{
			if(player.mo!=null)
			{
				if(wheelShow.Size()>0)
				{
					int curSelMod = wheelShow[currentSelection];
					if(player.mo.FindInventory(wheelWeapons[curSelMod].weaponClass))
					{
						player.mo.A_SelectWeapon(wheelWeapons[curSelMod].weaponClass);
						S_Sound("menu/choose",5);
					}
				}
			}
			wheelSort = false;
			wheelIsOpen = false;
			if(isLevelFrozen) ThawActors();
			ThawPlayer(players[e.Player]);
		}
	}
	
	Override Bool InputProcess(InputEvent e)
	{
		int key1, key2, key3, key4;
		
		if (e.Type == InputEvent.Type_KeyDown && automapactive == false) // not when automap is active
		{
			[key1, key2] = bindings.getKeysForCommand("+attack");
			[key3, key4] = bindings.getKeysForCommand("+use");
			if(e.keyScan == key1 || e.keyScan == key2 || e.keyScan == key3 || e.keyScan == key4)
			{
				if (wheelIsOpen) SendNetworkEvent("pyweaponwheeloff");
			}
		}
		
		return false;
	}
}